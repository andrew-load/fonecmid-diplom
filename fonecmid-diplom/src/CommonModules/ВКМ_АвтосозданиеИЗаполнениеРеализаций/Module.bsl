#Область СлужебныеПроцедурыИФункции
Процедура СформироватьАктыНаСервере(Параметры, АдресРезультата) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ДоговорыКонтрагентов.Владелец,
	|	ДоговорыКонтрагентов.Организация
	|ПОМЕСТИТЬ вт_ДействующиеДоговора
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора <= &ДатаДокумента
	|	И ДоговорыКонтрагентов.ВКМ_ОкончаниеДействияДоговора >= &ДатаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДействующиеДоговора.Договор КАК Договор,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Дата <= &КонецМесяца
	|		И РеализацияТоваровУслуг.Дата >= &НачалоМесяца
	|			ТОГДА РеализацияТоваровУслуг.Ссылка
	|	КОНЕЦ) КАК Реализация,
	|	вт_ДействующиеДоговора.Владелец,
	|	вт_ДействующиеДоговора.Организация
	|ИЗ
	|	вт_ДействующиеДоговора КАК вт_ДействующиеДоговора
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО вт_ДействующиеДоговора.Договор = РеализацияТоваровУслуг.Договор
	|СГРУППИРОВАТЬ ПО
	|	вт_ДействующиеДоговора.Договор,
	|	вт_ДействующиеДоговора.Владелец,
	|	вт_ДействующиеДоговора.Организация";
	Запрос.УстановитьПараметр("ДатаДокумента", Параметры.ДатаДокумента);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Параметры.ДатаДокумента));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Параметры.ДатаДокумента));

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Реализация) Тогда
			ОбъектРеализации = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			СтруктураОбъекта = Новый Структура("Дата, Контрагент, Организация, Договор, Основание",
				Параметры.ДатаДокумента, ВыборкаДетальныеЗаписи.Владелец, ВыборкаДетальныеЗаписи.Организация,
				ВыборкаДетальныеЗаписи.Договор, ВыборкаДетальныеЗаписи.Договор);
			ОбъектРеализации.Заполнить(СтруктураОбъекта);
			ОбъектРеализации.ВыполнитьАвтозаполнение();
			ОбъектРеализации.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры
#КонецОбласти