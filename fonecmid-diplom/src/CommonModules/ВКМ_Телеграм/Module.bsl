#Область ПрограммныйИнтерфейс

Функция ОтправитьСообщениеБоту(ТекстСообщения) Экспорт

	ТокенБота = Константы.ВКМ_ТокенУправленияТелаграмБотом.Получить();
	ИдентификаторЧата = Константы.ВКМ_ИдентификаторГруппыОповещения.Получить();
	Ресурс = СтрШаблон("bot%1/sendMessage?chat_id=%2&text=%3", ТокенБота, ИдентификаторЧата, ТекстСообщения);
	Хост = "api.telegram.org";

	SSL = Новый ЗащищенноеСоединениеOpenSSL;
	HTTPСоединение = Новый HTTPСоединение(Хост, , , , , , SSL);

	HTTPЗапрос = Новый HTTPЗапрос(Ресурс);
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос);
	Если HTTPОтвет.КодСостояния <> 200 Тогда
		ТелоОтвет = HTTPОтвет.ПолучитьТелоКакСтроку();
		ЗаписьЖурналаРегистрации("HTTPСервисы.Ошибка", УровеньЖурналаРегистрации.Ошибка, , , ТелоОтвет);
	КонецЕсли;
	
	Возврат HTTPОтвет.КодСостояния
		
КонецФункции

Процедура ОтправкаИУдалениеУведомленийВТелеграмБот() Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения,
		|	ВКМ_УведомленияТелеграмБоту.Ссылка
		|ИЗ
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Ответ = ОтправитьСообщениеБоту(ВыборкаДетальныеЗаписи.ТекстСообщения);
		Если Ответ = 200 Тогда
			ОбъектУведомления = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			ОбъектУведомления.Удалить();
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры 	
		

Функция ОбработатьВходящийЗапрос(Запрос) Экспорт
	
	ДанныеЗапроса = ДанныеЗапроса(Запрос);
	
	Если ДанныеЗапроса.Свойство("message") Тогда
		Возврат ОбработатьСообщение(ДанныеЗапроса);
	КонецЕсли;

	Возврат ВКМ_ОбщегоНазначенияHTTP.ПростойУспешныйОтвет();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеЗапроса(Запрос)
	
	ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
	Возврат ВКМ_ОбщегоНазначенияHTTP.ОбъектJSON(ТелоЗапроса);
	
КонецФункции

Функция ОбработатьСообщение(ДанныеСообщения)

	Если ДанныеСообщения.Свойство("from") Тогда
		 ЗафиксироватьОтправителя(ДанныеСообщения.from);
	КонецЕсли;
	
	ИдентификаторЧата = ДанныеСообщения.chat.id;
	
	Если ДанныеСообщения.Свойство("text") Тогда
		ТекстСообщения = ДанныеСообщения.text;
	Иначе	
		ТекстСообщения = "Пустое сообщение";	
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("methot", "sendMessage");
	ДанныеОтвета.Вставить("chat_id", ИдентификаторЧата);
	ДанныеОтвета.Вставить("text", ТекстСообщения);
	
	Возврат ВКМ_ОбщегоНазначенияHTTP.ОтветJSON(ДанныеОтвета);
	
КонецФункции

Процедура ЗафиксироватьОтправителя(ДанныеОтправителя)
	
	Идентификатор = ДанныеОтправителя.id;
	
	УчетнаяЗапись = Справочники.ВКМ_УчетныеЗаписиТелеграм.НайтиПоКоду(Идентификатор);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;	
	
	ЧастиИмениПользователя = Новый Массив;
	
	Если ДанныеОтправителя.Свойство("first_name") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.first_name);	
	КонецЕсли;	
	
	Если ДанныеОтправителя.Свойство("last_name") Тогда	
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.last_name);	
	КонецЕсли;
	
	Если ДанныеОтправителя.Свойство("username") Тогда	
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.username);	
	КонецЕсли;
	
	ИмяПользователя = СтрСоединить(ЧастиИмениПользователя, "");
	
	СпрОбъект = Справочники.ВКМ_УчетныеЗаписиТелеграм.СоздатьЭлемент();
	СпрОбъект.Наименование = ИмяПользователя;
	СпрОбъект.Код = Идентификатор;
	СпрОбъект.Записать();
	
КонецПроцедуры

#КонецОбласти