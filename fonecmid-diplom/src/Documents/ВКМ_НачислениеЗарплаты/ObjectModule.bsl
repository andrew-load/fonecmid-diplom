#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ,Режим)
	
	// регистр ВКМ_ОсновныеНачисления
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	Движения.ВКМ_Удержания.Записывать = Истина;
	Для Каждого ТекСтрокаОсновныеНачисления из ОсновныеНачисления Цикл
		
		Если Не ЗначениеЗаполнено(ТекСтрокаОсновныеНачисления.ПроцентОтРабот) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Просьба заполнить поле ""Процент от работ""";
			ПолеСтроки = СтрШаблон("ОсновныеНачисления[%1].ПроцентОтРабот",ТекСтрокаОсновныеНачисления.НомерСтроки - 1);
			Сообщение.Поле = ПолеСтроки;
			Сообщение.КлючДанных = Ссылка;
			Сообщение.ПутьКДанным =  "Объект";
			Сообщение.Сообщить();
			Отказ = Истина;
	
		КонецЕсли;
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ГрафикРаботы = ТекСтрокаОсновныеНачисления.ГрафикРаботы;
		Движение.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
		Движение.ПериодРегистрации = ТекСтрокаОсновныеНачисления.ДатаОкончания;
		Движение.ПериодДействияНачало = ТекСтрокаОсновныеНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = ТекСтрокаОсновныеНачисления.ДатаОкончания;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;
		Движение.Показатель = ТекСтрокаОсновныеНачисления.Оклад;
		
		Если ТекСтрокаОсновныеНачисления.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
			Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(ТекСтрокаОсновныеНачисления.ДатаНачала, -12));
			Движение.БазовыйПериодКонец = КонецМесяца(ТекСтрокаОсновныеНачисления.ДатаОкончания);
			Движение.ОтработаноДней = ((ТекСтрокаОсновныеНачисления.ДатаОкончания
				- ТекСтрокаОсновныеНачисления.ДатаНачала) / 86400) + 1;
		КонецЕсли;
		
		// регистр ВКМ_Удержания
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.ПериодРегистрации = ТекСтрокаОсновныеНачисления.ДатаОкончания;
		Движение.БазовыйПериодНачало = ТекСтрокаОсновныеНачисления.ДатаОкончания;
		Движение.БазовыйПериодКонец = ТекСтрокаОсновныеНачисления.ДатаОкончания;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;
		
		// регистр ВКМ_ВзаиморасчетыССотрудниками
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;
		
		УсловияОплаты = РегистрыСведений.ВКМ_УсловияОплатыСотрудников.СоздатьМенеджерЗаписи();
		УсловияОплаты.Период = ТекСтрокаОсновныеНачисления.ДатаОкончания;
		УсловияОплаты.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;	
		УсловияОплаты.Оклад = ТекСтрокаОсновныеНачисления.Оклад;
		УсловияОплаты.ПроцентОтРабот = ТекСтрокаОсновныеНачисления.ПроцентОтРабот;
		УсловияОплаты.Записать();
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	Движения.ВКМ_Удержания.Записать();
	
	РассчитатьОклад(); 
	РассчитатьОтпускные();
	РассчитатьУдержание();
	РассчитатьВзаиморасчеты();
	
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	//Данный фрагмент построен конструктором.
	//При повторном использовании конструктора, внесенные вручную данные будут утеряны!



	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура РассчитатьОклад()
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК ФактическиОтработанныхДней,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК КоличествоРабочихДнейВПериоде,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник,
		|	ОсновныеНачисленияДанныеГрафика.ОтработаноДней
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(ВидРасчета = &Оклад
		|	И Регистратор = &Ссылка) КАК ОсновныеНачисленияДанныеГрафика";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1]; 
		
		ДневнаяСтавка = Движение.Показатель / ВыборкаДетальныеЗаписи.КоличествоРабочихДнейВПериоде;
		
		Движение.Результат = (ДневнаяСтавка * ВыборкаДетальныеЗаписи.ФактическиОтработанныхДней); 
		Движение.ОтработаноДней = ВыборкаДетальныеЗаписи.ФактическиОтработанныхДней;
								
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	Движения.ВКМ_Удержания.Записать();
КонецПроцедуры


Процедура РассчитатьОтпускные()
		
			
Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.Результат, 0) КАК БазаОсн,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.ОтработаноДнейБаза, 0) КАК ОтработаноДней,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ФактическийОтработаноДней,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеБазовыйПериод КАК КоличествоРабочихДнейВПериоде,
		|	ОсновныеНачисленияБазаОсновныеНачисления.БазовыйПериодКонец,
		|	ОсновныеНачисленияБазаОсновныеНачисления.БазовыйПериодНачало,
		|	ОсновныеНачисленияБазаОсновныеНачисления.ПериодДействия,
		|	ОсновныеНачисленияБазаОсновныеНачисления.ПериодДействияКонец,
		|	ОсновныеНачисленияБазаОсновныеНачисления.ПериодДействияНачало,
		|	ОсновныеНачисленияБазаОсновныеНачисления.ПериодРегистрации,
		|	ОсновныеНачисленияБазаОсновныеНачисления.Результат,
		|	ОсновныеНачисленияБазаОсновныеНачисления.Сотрудник,
		|	ОсновныеНачисленияБазаОсновныеНачисления.ОтработаноДней КАК ДнейОтпускаФакт,
		|	ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза КАК РезультатБаза,
		|	ОсновныеНачисленияБазаОсновныеНачисления.Показатель,
		|	ОсновныеНачисленияБазаОсновныеНачисления.ОтработаноДнейБаза
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,,
		|			Регистратор = &Ссылка
		|		И ВидРасчета = &Отпуск) КАК ОсновныеНачисленияБазаОсновныеНачисления
		|		ПО ОсновныеНачисления.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ПО ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки
		|ГДЕ
		|	ОсновныеНачисления.Регистратор = &Ссылка
		|	И ОсновныеНачисления.ВидРасчета = &Отпуск";
	
	БазовыйПериодНачало = ДобавитьМесяц(Дата, -12);
	БазовыйПериодКонец = Дата;
	
	Запрос.УстановитьПараметр("БазовыйПериодНачало", БазовыйПериодНачало);
	Запрос.УстановитьПараметр("БазовыйПериодКонец", БазовыйПериодКонец);
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		
		Если ВыборкаДетальныеЗаписи.ОтработаноДнейБаза = 0 Тогда
			Движение.Результат = 0;
			Продолжить;
		КонецЕсли;
			
		Движение.Показатель = ВыборкаДетальныеЗаписи.РезультатБаза / ВыборкаДетальныеЗаписи.ОтработаноДнейБаза;
		Результат = (ВыборкаДетальныеЗаписи.РезультатБаза / ВыборкаДетальныеЗаписи.ОтработаноДнейБаза) * ВыборкаДетальныеЗаписи.ДнейОтпускаФакт; 
		Движение.Результат = Результат; 
			
	КонецЦикла;	
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура РассчитатьУдержание()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
				   |	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.Регистратор КАК Регистратор,
				   |	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
				   |	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РезультатБаза КАК РезультатБаза
				   |ИЗ
				   |	РегистрРасчета.ВКМ_Удержания.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК ВКМ_УдержанияБазаВКМ_ОсновныеНачисления";
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВКМ_Удержания[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		Движение.Результат = ВыборкаДетальныеЗаписи.РезультатБаза;	
	КонецЦикла;

	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура РассчитатьВзаиморасчеты()
		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_ОсновныеНачисления.Результат КАК Начисление,
		|	ВКМ_Удержания.Результат КАК Удержение,
		|	ВКМ_ОсновныеНачисления.Результат - ВКМ_Удержания.Результат КАК Результат,
		|	ВКМ_ОсновныеНачисления.НомерСтроки
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_Удержания.НомерСтроки
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
		|	И ВКМ_Удержания.Регистратор = &Регистратор";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			// регистр ВКМ_ВзаиморасчетыССотрудниками
			Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками[ВыборкаДетальныеЗаписи.НомерСтроки - 1];		
			Движение.Сумма = ВыборкаДетальныеЗаписи.Результат;	
	КонецЦикла;

	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
КонецПроцедуры	

#КонецОбласти	
